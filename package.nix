# Heimcloud shop OCI image — buildNpmPackage (better-sqlite3) + dockerTools.
# callPackage from perSystem or the nixos module; Neo loads via imageFile.
{
  lib,
  dockerTools,
  buildNpmPackage,
  nodejs_22,
  python3,
  pkg-config,
  sqlite,
  cacert,
  tzdata,
  fakeNss,
  srcOnly,
  removeReferencesTo,
}: let
  nodejs = nodejs_22;
  nodeSources = srcOnly nodejs;

  app = buildNpmPackage {
    pname = "heimcloud-shop";
    version = "0.1.0";
    src = lib.cleanSourceWith {
      src = ./app;
      filter = path: type: let
        base = baseNameOf path;
      in
        base
        != "node_modules"
        && base != ".env"
        && base != "data"
        && !(lib.hasSuffix ".sqlite" path)
        && !(lib.hasSuffix ".sqlite-wal" path)
        && !(lib.hasSuffix ".sqlite-shm" path);
    };
    npmDepsHash = "sha256-9OHfwDjVDSXaSXgldK3Vzy2kQE1i9y0BqLQFvzrZdzI=";
    inherit nodejs;
    dontNpmBuild = true;
    nativeBuildInputs = [python3 pkg-config removeReferencesTo];
    buildInputs = [sqlite];

    # Compile better-sqlite3 against nix node headers (prebuilds may not match).
    postBuild = ''
      pushd node_modules/better-sqlite3
      npm run build-release --offline --nodedir="${nodeSources}"
      find build -type f -exec remove-references-to -t "${nodeSources}" {} \;
      popd
    '';

    installPhase = ''
      runHook preInstall
      mkdir -p $out/app
      cp -r server.js package.json lib public node_modules $out/app/
      if [ -d views ]; then cp -r views $out/app/; fi
      runHook postInstall
    '';
  };
in
  dockerTools.buildLayeredImage {
    name = "heimcloud-shop";
    tag = "latest";
    contents = [
      nodejs
      app
      cacert
      tzdata
      fakeNss
      dockerTools.caCertificates
    ];
    extraCommands = ''
      mkdir -p data
    '';
    config = {
      WorkingDir = "/app";
      Env = [
        "NODE_ENV=production"
        "PORT=3000"
        "TZ=Europe/Zurich"
        "SHOP_DB_PATH=/data/shop.sqlite"
        "SSL_CERT_FILE=/etc/ssl/certs/ca-bundle.crt"
      ];
      ExposedPorts = {
        "3000/tcp" = {};
      };
      Volumes = {
        "/data" = {};
      };
      # No USER — Neo OCI `user` (neo.core.uid:gid) is the source of truth.
      Cmd = ["${nodejs}/bin/node" "/app/server.js"];
    };
  }
